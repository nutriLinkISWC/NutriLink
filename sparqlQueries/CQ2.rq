PREFIX graph: <http://example.org/#>
PREFIX vaem: <http://www.linkedmodel.org/schema/vaem#>
PREFIX qudt: <http://qudt.org/schema/qudt#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

#basket is the unique identifier
#because the date is sanitized to only the day, not the exact timestamp.

select
distinct ?basket ?date ?itemName ?itemQuantity ?itemPrice ?productQuantityValue
?ofComValue ?ofComNEnergyKJ ?ofComNSugar ?ofComNSaturatedFat ?ofComNSodium ?ofComPFVPN ?ofComPDietaryFiber 
?ofComPProtein ?nutrientName ?nutrientValue
where {

?basket graph:containsItem ?item FILTER (?basket = <BASKET_ID>).
?basket graph:timestamp ?date.
?item graph:amount ?itemQuantity.
?item graph:simplePrice ?itemPrice.
?item vaem:name ?itemName.
	
optional
    {
    ?item graph:matches ?product.

    #In this way, it is faster to get the values, but each nutrient takes one row
    #All nutrients are of unit qudt:GM
    ?nutrient a graph:Nutrient.
    ?product graph:containsNutritionInformation ?nutrient.
    ?nutrient vaem:name ?nutrientName  filter (?nutrientName != "FVPN").
    ?nutrient qudt:hasQuantity ?nutrientQuantity.
    ?nutrientQuantity qudt:value ?nutrientValue.

	?product graph:containsNutritionInformation ?ofComDetail.
    ?ofComDetail graph:ofComValue ?ofComValue.
	?ofComDetail graph:ofComNEnergyKJ ?ofComNEnergyKJ.
	?ofComDetail graph:ofComNSugar ?ofComNSugar.
	?ofComDetail graph:ofComNSaturatedFat ?ofComNSaturatedFat.
	?ofComDetail graph:ofComNSalt ?ofComNSodium.
	?ofComDetail graph:ofComPFVPN ?ofComPFVPN.
	?ofComDetail graph:ofComPDietaryFiber ?ofComPDietaryFiber.
	?ofComDetail graph:ofComPProtein ?ofComPProtein.

    #All product are of unit qudt:GM
    ?product qudt:hasQuantity ?productQuantity.
    ?productQuantity qudt:value ?productQuantityValue.
    }
}
